include builtins\bigatom.e

sequence lf_list

procedure init(integer n)
bigatom f = ba_new(1)
atom t1 = time()+1
    lf_list = repeat(f,n+1)
    for i=1 to n do
        f = ba_multiply(f,i)
        lf_list[i+1] = f
        if time()>t1 then
            printf(1,"loading main table (%d of %d)...\n",{i,n})
            t1 = time()+1
        end if
    end for
end procedure

function lf(integer n)
-- Returns left factorial of n, as a string
bigatom sumf = ba_new(0)
    for k=0 to n-1 do sumf = ba_add(sumf, lf_list[k+1]) end for
    return ba_sprint(sumf)
end function

-- Main procedure
atom t0 = time()
init(10000)
for i=0 to 10 do printf(1,"!%d = %s\n",{i,lf(i)}) end for
for i=20 to 110 by 10 do printf(1,"!%d = %s\n",{i,lf(i)}) end for
for i=1000 to 10000 by 1000 do printf(1,"!%d contains %d digits\n",{i,length(lf(i))}) end for
printf(1,"complete (%3.2fs)\n",{time()-t0})
