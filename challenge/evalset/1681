function hamming(integer N)
sequence h = repeat(1,N)
atom x2 = 2, x3 = 3, x5 = 5, hn
integer i = 1, j = 1, k = 1
    for n=2 to N do
        hn = min(x2,min(x3,x5))
        h[n] = hn
        if hn=x2 then i += 1 x2 = 2*h[i] end if
        if hn=x3 then j += 1 x3 = 3*h[j] end if
        if hn=x5 then k += 1 x5 = 5*h[k] end if
    end for
    return h[N]
end function

include builtins\bigatom.e

function ba_min(bigatom a, bigatom b)
    return iff(ba_compare(a,b)<0?a:b)
end function

function ba_hamming(integer N)
sequence h = repeat(ba_new(1),N)
bigatom x2 = ba_new(2), x3 = ba_new(3), x5 = ba_new(5), hn
integer i = 1, j = 1, k = 1
    for n=2 to N do
        hn = ba_min(x2,ba_min(x3,x5))
        h[n] = hn
        if ba_compare(hn,x2)=0 then i += 1 x2 = ba_multiply(2,h[i]) end if
        if ba_compare(hn,x3)=0 then j += 1 x3 = ba_multiply(3,h[j]) end if
        if ba_compare(hn,x5)=0 then k += 1 x5 = ba_multiply(5,h[k]) end if
    end for
    return h[N]
end function

sequence s = {}
for i=1 to 20 do
    s = append(s,hamming(i))
end for
?s
?hamming(1691)
?{hamming(1000000),"wrong!"}

?ba_sprintf("%B\n",ba_hamming(1691))
?ba_sprintf("%B\n",ba_hamming(1000000))
